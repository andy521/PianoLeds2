#!python

import platform

# Create base environment
Import('base_env')
env = base_env.Clone()
symbols = [
]
flags = [
    '-Wall',
    '-std=c++11',
    '-Os',
    '-g3',
    '-fdata-sections',
    '-ffunction-sections',
]
link_flags = [
    '-Wl,-dead_strip',
]

if platform.system() == 'Darwin':
    link_flags += [
        '-F/System/Library/Frameworks',
        '-Wl,-framework,CoreMIDI',
        '-Wl,-framework,CoreAudio',
        '-Wl,-framework,CoreFoundation',
    ]
    symbols += [
        '__MACOSX_CORE__',
    ]
else:
    raise Exception('Platform for RtMidi not configured or not supported')

env.Append(CDEFINES=symbols)
env.Append(CPPDEFINES=symbols)
env.Append(CFLAGS=flags)
env.Append(CXXFLAGS=flags)
env.Append(LINKFLAGS=link_flags)
env.Append(CPPPATH=[
    '#',
    '#/ExternalComponents',
    '#/ExternalComponents/googletest/googletest/include',
    '#/ExternalComponents/googletest/googlemock/include',
    '/usr/local/Cellar/nlohmann_json/2.1.1/include',
])

rtmidi = SConscript('ExternalComponents/RtMidi.scons',
                    exports={'base_env': env},
                    duplicate=False)
drivers = SConscript('Drivers/PC.scons',
                     exports={'base_env': env},
                     duplicate=False)

common = SConscript('Common/Common.Scons',
                    exports={'base_env': env},
                    duplicate=False)

# Build MidiInputMonitor test app against the libraries
monitor = SConscript('MidiInputMonitor/MidiInputMonitor.scons',
                     exports={'base_env': env, 'libs': [rtmidi, drivers]},
                     duplicate=False)
